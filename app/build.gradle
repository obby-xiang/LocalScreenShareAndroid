plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.protobuf)
}

def signingProperties = new Properties()
def signingPropertiesFile = rootProject.file("signing.properties")
if (signingPropertiesFile.exists()) {
    signingProperties.load(new FileInputStream(signingPropertiesFile))
} else {
    signingProperties['storeFile'] = property('signing.storeFile')
    signingProperties['storePassword'] = property('signing.storePassword')
    signingProperties['keyAlias'] = property('signing.keyAlias')
    signingProperties['keyPassword'] = property('signing.keyPassword')
}

android {
    namespace 'com.obby.android.localscreenshare'
    compileSdk 35

    defaultConfig {
        applicationId "com.obby.android.localscreenshare"
        minSdk 25
        targetSdk 35
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile rootProject.file(signingProperties['storeFile'])
            storePassword signingProperties['storePassword']
            keyAlias signingProperties['keyAlias']
            keyPassword signingProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.5"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.71.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { option 'lite' }
            }
        }
    }
}

dependencies {

    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

    coreLibraryDesugaring libs.desugar.jdk.libs
    compileOnly libs.lombok
    annotationProcessor libs.lombok
    implementation libs.grpc.okhttp
    implementation libs.grpc.protobuf.lite
    implementation libs.grpc.stub
    compileOnly libs.tomcat.annotations.api
    implementation libs.commons.lang3
    implementation libs.guava
    implementation libs.camera.core
}
